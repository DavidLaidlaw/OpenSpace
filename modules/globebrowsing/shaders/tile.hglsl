/*****************************************************************************************
 *                                                                                       *
 * OpenSpace                                                                             *
 *                                                                                       *
 * Copyright (c) 2014-2018                                                               *
 *                                                                                       *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this  *
 * software and associated documentation files (the "Software"), to deal in the Software *
 * without restriction, including without limitation the rights to use, copy, modify,    *
 * merge, publish, distribute, sublicense, and/or sell copies of the Software, and to    *
 * permit persons to whom the Software is furnished to do so, subject to the following   *
 * conditions:                                                                           *
 *                                                                                       *
 * The above copyright notice and this permission notice shall be included in all copies *
 * or substantial portions of the Software.                                              *
 *                                                                                       *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,   *
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A         *
 * PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT    *
 * HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF  *
 * CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE  *
 * OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                                         *
 ****************************************************************************************/

#ifndef TEXTURETILE_HGLSL
#define TEXTURETILE_HGLSL

struct TileDepthTransform {
    float depthScale;
    float depthOffset;
};

struct TileUvTransform {
    vec2 uvOffset;
    vec2 uvScale;
};

struct ChunkTile {
    sampler2D textureSampler;
    TileUvTransform uvTransform;
};

struct PixelPadding {
    ivec2 startOffset;
    ivec2 sizeDifference;
};

struct ChunkTilePile {
    ChunkTile chunkTile0;
    ChunkTile chunkTile1;
    ChunkTile chunkTile2;
};

struct LayerSettings {
    float opacity;
    float gamma;
    float multiplier;
    float offset;
    float valueBlending;
};

struct LayerAdjustment {
    vec3 chromaKeyColor;
    float chromaKeyTolerance;
};

struct Layer {
    ChunkTilePile pile;
    TileDepthTransform depthTransform;
    LayerSettings settings;
    LayerAdjustment adjustment;
    PixelPadding padding;
    
    // Other layer type properties stuff
    vec3 color;
};

vec2 compensateSourceTextureSampling(vec2 startOffset, vec2 sizeDiff, ChunkTile chunkTile,
                                     vec2 tileUV)
{
    ivec2 resolution = textureSize(chunkTile.textureSampler, 0);

    vec2 sourceSize = vec2(resolution) + sizeDiff;
    vec2 currentSize = vec2(resolution);
    vec2 sourceToCurrentSize = currentSize / sourceSize;
    return sourceToCurrentSize * (tileUV - startOffset / sourceSize);
}

vec2 tileUVToTextureSamplePosition(ChunkTile chunkTile, vec2 tileUV,
                                   PixelPadding padding)
{
    vec2 uv = chunkTile.uvTransform.uvOffset + chunkTile.uvTransform.uvScale * tileUV;
    return compensateSourceTextureSampling(
        padding.startOffset,
        padding.sizeDifference,
        chunkTile,
        uv
    );
}

float getLevelInterpolationParameter(int chunkLevel, float distanceScaleFactor,
                                     float distToVertexOnEllipsoid)
{
    float projectedScaleFactor = distanceScaleFactor / distToVertexOnEllipsoid;
    float desiredLevel = log2(projectedScaleFactor);
    return chunkLevel - desiredLevel;
}

vec3 getLevelWeights(float levelInterp) {
    return clamp(
        vec3(
            1.0 - levelInterp,
            clamp(levelInterp, 0.0, 1.0) - clamp(levelInterp - 1.0, 0.0, 1.0),
            levelInterp - 1.0),
        vec3(0.0),
        vec3(1.0)
    );
}

vec4 getTexVal(ChunkTilePile chunkTilePile, vec3 w, vec2 uv,
               PixelPadding padding)
{
    vec4 v1 = texture(
        chunkTilePile.chunkTile0.textureSampler,
        tileUVToTextureSamplePosition(chunkTilePile.chunkTile0, uv, padding)
    );
    vec4 v2 = texture(
        chunkTilePile.chunkTile1.textureSampler,
        tileUVToTextureSamplePosition(chunkTilePile.chunkTile1, uv, padding)
    );
    vec4 v3 = texture(
        chunkTilePile.chunkTile2.textureSampler,
        tileUVToTextureSamplePosition(chunkTilePile.chunkTile2, uv, padding)
    );

    return w.x * v1 + w.y * v2 + w.z * v3;
}

#endif // TEXTURETILE_HGLSL
